#!/usr/bin/perl

use warnings;
use strict;

my %resource = ();
my @state = ();
my @config_lines= ();

my $cfgpath = "/opt/rudder/etc/rudderc.conf";
if (@ARGV) {
  ($cfgpath) = @ARGV;
}
# ultralight homemade parser
my ($ncfpath, $stdlibpath, $generic_methods) = get_paths_from_conf_file($cfgpath);
my $ncf_methods = $ncfpath . "/tree/30_generic_methods/*";
my $is_empty=1;
foreach my $file (glob $ncf_methods) {
  $is_empty=0;
  # file exclusion
  next if $file=~ /\/_/;
  next if $file=~ /README/;
  next if $file=~ /permissions.cf/;

  open(my $fd, "<$file") or die "cannot read $file";

  my $resource_blocks=1;
  my $resource_identifiers=1;
  if ($file =~ /\/http_request_/) { $resource_identifiers=2; }
  if ($file =~ /\/sharedfile_/) { $resource_identifiers=2; }
  if ($file =~ /\/variable_/) { $resource_identifiers=2; }
  # exception's exception: only one parameter for these resources, TODO hack for translate
  if ($file =~ /\/variable_string_escaped/) { next; }

  # init data
  my $i=0;
  my ($resource, $state, $prefix, $class_parameter) = ("", "", "", -1);
  my $gm = "";
  my @resource_params=();
  my @state_params=();
  my %param_id=();

  # loop over GM
  while(my $line=<$fd>)
  {
    if ($line =~ /^#.*\@parameter\s+(\w+)\s.*$/) {
      $param_id{$1}=$i;
      if ($i < $resource_identifiers) {
        push @resource_params, "p$i";
      } else {
        push @state_params, "p$i";
      }
      $i++;
    }
    elsif($line =~ /^#.*\@class_prefix\s+(\w+)/) {
      $prefix = $1;
    }
    elsif($line =~ /^#.*\@class_parameter\s+(\w+)/) {
      $class_parameter = $param_id{$1};
    }
    elsif($line =~ /^bundle agent (\w+)\(/) {
      $gm = $1;
      # exception for kernel that should be kernel_module (took from state)
      my $res_type = "[a-zA-Z0-9]+";
      if ($1 =~ /kernel_module/) {
        $res_type = "kernel_module";
      }
      # exception for http resource that should be http_request (took from state)
      elsif ($1 =~ /http_request/) {
        $res_type = "http_request";
      }
      $gm =~ /^((?:$res_type\_){$resource_blocks})(\w+)$/;
      ($resource, $state) = ($1, $2);
      chop $resource;
    }
  }

  # keep output
  $resource{"resource $resource(".join(",",@resource_params).")"}=1;
  push @state, "$resource state $state(".join(",",@state_params)."){}";
  push @config_lines, "$gm = { class_prefix=\"$prefix\", class_parameter_id = $class_parameter }";
}

if ($is_empty) {
  print "no ncf method found for $ncf_methods";
}

open(my $resourcelib, '>', "$stdlibpath/resourcelib.rl") or die "Cannot write resourcelib ($stdlibpath/resourcelib.rl)";
print $resourcelib "\@format=0";
print $resourcelib "\n\n";
print $resourcelib join("\n",keys %resource);
print $resourcelib "\n\n";
print $resourcelib join("\n", @state);
print $resourcelib "\n\n";
close($resourcelib);

my $config_header = "[resources]
http_request = { parameter_count=2 }
sharedfile = { parameter_count=2 }
variable = { parameter_count=2 }
[classes]
command_execution = \"(command_execution)_(.*)\"
[variables]
[conditions]
[methods]
";
open(my $conf, '>', $generic_methods) or die "Cannot write methods config ($generic_methods)";
print $conf join("\n",$config_header);
print $conf join("\n",@config_lines);
close($conf);


sub get_paths_from_conf_file {
  my ($file) = @_;

  my $is_default = 0;
  my $ncf = "/usr/share/ncf";
  my $stdlibpath = "/opt/rudder/share/rudder-lang/lib";
  my $generic_methods = "/opt/rudder/share/rudder-lang/tools/generic_methods.toml";
  open (FILE, $file);
  while (<FILE>) {
    chomp;
    if ($is_default == 1 && /\s*ncf\s*=\s*"(?<path>[\/\w.-]+)"\s*/ ) {
      $ncf = $+{path};
    }
    if ($is_default == 1 && /\s*stdlib\s*=\s*"(?<path>[\/\w.-]+)"\s*/ ) {
      $stdlibpath = $+{path};
    }
    if ($is_default == 1 && /\s*generic_methods\s*=\s*"(?<path>[\/\w.-]+)"\s*/ ) {
      $generic_methods = $+{path};
    }
    if ( /\s*\[\s*(?<group>\w+)\s*\]\s*/ ) {
      if ($+{group} eq "shared") {
        $is_default = 1;
      } else {
        $is_default = 0;
      }
    }
  }
  close (FILE);
  return ($ncf, $stdlibpath, $generic_methods);
}
